apply plugin: 'com.android.library'
apply from: '../prepare-robolectric.gradle'


def DEFAULT_COMPILE_SDK_VERSION             = 25
def DEFAULT_BUILD_TOOLS_VERSION             = "26.0.2"
def DEFAULT_TARGET_SDK_VERSION              = 23
def DEFAULT_ANDROID_SUPPORT_VERSION          = "25.3.1"

android {
    compileSdkVersion project.hasProperty('compileSdkVersion') ? project.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
    buildToolsVersion project.hasProperty('buildToolsVersion') ? project.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion project.hasProperty('targetSdkVersion') ? project.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.all { t ->
            reports {
                html.enabled true
            }
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "      ${result.resultType} (${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)     "
                    def repeatLength = output.length()
                    println '\n' + ('-' * repeatLength) + '\n' + output + '\n' + ('-' * repeatLength) + '\n'

                    println "see report at file://${t.reports.html.destination}/index.html"
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: "libs", include: ["*.jar"])

    def androidSupportVersion = project.hasProperty('androidSupportVersion') ? project.androidSupportVersion : DEFAULT_ANDROID_SUPPORT_VERSION

	  compile "com.android.support:design:$androidSupportVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"

    // node_modules
    compile "com.facebook.react:react-native:+"

    // third party
    compile "com.aurelhubert:ahbottomnavigation:2.0.6"
    compile 'com.balysv.materialmenu:material-menu-toolbar:1.5.4'

    // tests
    testCompile "junit:junit:4.12"
    testCompile "org.robolectric:robolectric:3.1.4"
    testCompile 'org.assertj:assertj-core:2.5.0'
}
