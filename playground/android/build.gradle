// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: './rninfo.gradle'

    ext {
        // RN 0.79+ uses Kotlin 2.1.x, older versions use 2.0.21
        kotlinVersion = ext.rnInfo.isRN79OrHigher ? '2.1.20' : '2.0.21'
        RNNKotlinVersion = kotlinVersion
        detoxKotlinVersion = kotlinVersion
        compileSdkVersion = 35
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        targetSdkVersion = 35
        ndkVersion = "27.1.12297006"
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath 'com.android.tools.build:gradle'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven { url "$rootDir/../node_modules/detox/Detox-android" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        flatDir {
            dirs "$rootProject.projectDir/libs"
        }
        mavenCentral()
    }
}

subprojects {
    afterEvaluate { p ->
        if (p.hasProperty('android')) {
            android {
                buildToolsVersion rootProject.ext.buildToolsVersion
            }
        }
    }
}
